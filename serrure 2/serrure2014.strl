% ;;ENTREE(1);ENTREE(22);;;;
% ;;;ENTREE(12345);;;;;ENTREE(1);;;ENTREE(22);;;
% ;;ENTREE(22);ENTREE(1);ENTREE(12345);ENTREE(123);ENTREE(4);ENTREE(5);;
% ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
module serrure2014 :
constant MAX_ESSAI = 3   : integer;
constant CLEF1 = 1       : integer;
constant CLEF2 = 22      : integer;
constant DUREE_ESSAI = 4 : integer;
input ENTREE             : integer;
output DEBUT;
output BLOQUE;
output OUVERT;
output TEMPS_DEPASSE;
output ERREUR;
output TEMPS             : integer;

trap FIN,SOS in
var nbErreurs:=0 : integer in
loop
  var entree1 : integer in
  var tempsReste:=DUREE_ESSAI : integer in
  emit DEBUT;
  await ENTREE;
  entree1:=?ENTREE;
  abort 
    repeat DUREE_ESSAI times
      pause;
      tempsReste := tempsReste-1;
      emit TEMPS(tempsReste);
    end repeat; 
  when ENTREE;
  if tempsReste<=0 then 
    emit TEMPS_DEPASSE
  else
    if entree1=CLEF1 and ?ENTREE=CLEF2 then 
      emit OUVERT;
      exit FIN
    else 
      nbErreurs:=nbErreurs+1;
      if nbErreurs >= MAX_ESSAI then
        exit SOS
      else
        emit ERREUR; 
      end if
    end if
  end if;
  end var
  end var
end loop
end var
handle SOS do sustain BLOQUE
end trap

end module





